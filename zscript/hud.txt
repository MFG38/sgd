class SGDStatusBar : BaseStatusBar
{
    HUDFont sfont, invfont, msgfont;
    InventoryBarState diparms;

    transient cvar finisherbar;
    transient cvar finisherprompt;
    transient cvar hazmatbar;

    override void Init()
    {
        super.Init();
        // TODO: Widescreen resolution support. Shouldn't be too hard.
        SetSize(0,320,240);

        Font fnt = "SGDHUDFONT";
        sfont = HUDFont.Create(fnt,0,Mono_Off,0,0);
        fnt = "SMALLFONT";
        invfont = HUDFont.Create(fnt,0,Mono_Off,0,0);
        msgfont = HUDFont.Create(fnt,0,Mono_Off,0,0);
        diparms = InventoryBarState.Create();
    }

    override void Draw(int state, double TicFrac)
    {
        super.Draw(state,TicFrac);

        if(!automapactive)
        {
            DrawHUD();
        }
    }

    void DrawHealthAndArmor()
    {
        DrawImage("HAFRAME",(52,232),DI_ITEM_LEFT_BOTTOM);
        DrawString(sfont,FormatNumber(CPlayer.health,3),(53,211),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT,Font.CR_RED);
        DrawString(sfont,FormatNumber(GetArmorAmount(),3),(89,211),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT,Font.CR_RED);
    }

    void DrawWeaponsAndAmmo()
    {
        DrawImage("AMMFRAME",(316,236),DI_ITEM_RIGHT_BOTTOM);
        DrawImage("WEPFRAME",(268,232),DI_ITEM_RIGHT_BOTTOM);

        Inventory ammo = GetCurrentAmmo();
        if(ammo != null)
        {
            DrawString(sfont,FormatNumber(ammo.Amount,3),(231,211),DI_ITEM_TOP|DI_TEXT_ALIGN_CENTER,Font.CR_RED);

            // Well, this is embarrassing. I spent three entire days wracking my brain with different ways to
            // render the ammo icons, all of which produced fatal errors, and the solution that finally works
            // is the simplest one possible. I tried to needlessly overcomplicate the method when *this* was
            // all I had to do. Credit goes to Jarewill for pointing me to this direction.
            DrawInventoryIcon(CPlayer.ReadyWeapon,(309,224),DI_ITEM_RIGHT_BOTTOM);
        }

        DrawImage(CPlayer.HasWeaponsInSlot(0)? "WEPLIGHT" : "",(257,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(1)? "WEPLIGHT" : "",(203,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(2)? "WEPLIGHT" : "",(209,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(3)? "WEPLIGHT" : "",(215,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(4)? "WEPLIGHT" : "",(221,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(5)? "WEPLIGHT" : "",(227,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(6)? "WEPLIGHT" : "",(233,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(7)? "WEPLIGHT" : "",(239,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(8)? "WEPLIGHT" : "",(245,226),DI_ITEM_LEFT_TOP);
        DrawImage(CPlayer.HasWeaponsInSlot(9)? "WEPLIGHT" : "",(251,226),DI_ITEM_LEFT_TOP);
    }

    void DrawFinisherBar()
    {
        finisherbar = CVar.FindCVar('sgd_displayfbar');
        bool drawfbar = finisherbar.GetBool();

        if(drawfbar)
        {
            DrawImage("FTBARFRM",(4,4),DI_ITEM_LEFT_TOP);
            DrawBar("FTBAR_FG","FTBAR_BG",GetAmount("FinisherToken"),GetMaxAmount("FinisherToken"),(10,10),0,0,DI_ITEM_LEFT_TOP);
        }
    }

    void DrawFinisherPrompt()
    {
        finisherprompt = CVar.FindCVar('sgd_finisherprompt');
        int prompttype = finisherprompt.GetInt();

        if(prompttype > 0)
        {
            if(CPlayer.mo.CountInv("FinisherToken") >= 300 && CPlayer.mo.CountInv("FinisherToken") < 600)
            {
                DrawString(msgfont,StringTable.Localize("$SGD_FMSG_LVL1READY"),(8,27),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT,Font.CR_GREEN);

                if(prompttype == 2)
                {
                    DrawString(msgfont,StringTable.Localize("$SGD_FMSG_USE"),(8,35),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT,Font.CR_GREEN);
                }
            }
            else if(CPlayer.mo.CountInv("FinisherToken") >= 600 && CPlayer.mo.CountInv("FinisherToken") < 900)
            {
                DrawString(msgfont,StringTable.Localize("$SGD_FMSG_LVL2READY"),(8,27),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT,Font.CR_GREEN);

                if(prompttype == 2)
                {
                    DrawString(msgfont,StringTable.Localize("$SGD_FMSG_USE"),(8,35),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT,Font.CR_GREEN);
                }
            }
            else if(CPlayer.mo.CountInv("FinisherToken") >= 900)
            {
                DrawString(msgfont,StringTable.Localize("$SGD_FMSG_LVL3READY"),(8,27),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT,Font.CR_GREEN);

                if(prompttype == 2)
                {
                    DrawString(msgfont,StringTable.Localize("$SGD_FMSG_USE"),(8,35),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT,Font.CR_GREEN);
                }
            }
        }
    }

    void DrawInvItem()
    {
        DrawImage("INVFRAME",(4,236),DI_ITEM_LEFT_BOTTOM);

        if(CPlayer.mo.InvSel != null)
        {
            DrawInventoryIcon(CPlayer.mo.InvSel,(10,224),DI_ITEM_LEFT_BOTTOM|DI_DIMDEPLETED);

            if(CPlayer.mo.InvSel.Amount > 1)
            {
                DrawString(invfont,FormatNumber(CPlayer.mo.InvSel.Amount,3),(11,194),DI_ITEM_LEFT_TOP|DI_TEXT_ALIGN_LEFT);
            }
        }
    }

    void DrawHazmatTimer()
    {
        hazmatbar = CVar.FindCVar('sgd_displayhsbar');
        bool drawhsbar = hazmatbar.GetBool();

        if(drawhsbar && CPlayer.mo.CountInv("RadSuitTimer") > 0)
        {
            DrawBar("HSBAR_FG","HSBAR_BG",GetAmount("RadSuitTimer"),GetMaxAmount("RadSuitTimer"),(312,8),0,1,DI_ITEM_RIGHT_TOP);
        }
    }

    // Only thing missing still.
    // void DrawKeyIcons() {}

    void DrawDemonizerTimers()
    {
        DrawBar("DBARL_FG","DBARL_BG",GetAmount("DemonizerTimer"),GetMaxAmount("DemonizerTimer"),(145,224),0,1,DI_ITEM_RIGHT_BOTTOM);
        DrawBar("DBARR_FG","DBARR_BG",GetAmount("DemonizerTimer"),GetMaxAmount("DemonizerTimer"),(175,224),0,0,DI_ITEM_LEFT_BOTTOM);
    }

    void DrawHUD()
    {
        if(CPlayer.mo.CountInv("DemonizerTimer") > 0)
        {
            DrawDemonizerTimers();
        }
        else
        {
            DrawHealthAndArmor();
            DrawWeaponsAndAmmo();
            DrawFinisherBar();
            DrawFinisherPrompt();
            DrawInvItem();
            DrawHazmatTimer();
        }
    }
}
