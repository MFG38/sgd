class SGDDarkMatterCannon : SGDWeapon
{
	Default
	{
		Weapon.SlotNumber 9;
		Weapon.SelectionOrder 999;
		Weapon.AmmoType "DMCAmmo";
		Weapon.AmmoGive 1;
		Weapon.AmmoUse 1;
		Tag "$SGD_WEP_DMCANNON";
		Inventory.PickupMessage "$SGD_WEP_DMCANNON";
		+INVENTORY.IGNORESKILL;
	}

	States
	{
	Spawn:
		DCAN A -1;
		Stop;
	Select:
		DMAT A 1 A_FastRaise;
		Goto Select;
	Deselect:
		DMAT A 1 A_FastLower;
		Goto Deselect;
	Ready:
		DMAT A 0;
		Goto ReadyLoop;
	Fire:
		DMAT A 45 A_DMCStart;
		DMAT B 3 A_DMCFire;
		DMAT C 3;
		DMAT D 3;
		DMAT A 10;
		Goto ReadyLoop;
	AltFire:	// Overriding this because finishers are unnecessary for this weapon.
		DMAT A 0;
		Goto ReadyLoop;
	}
}

class DarkMatterProjectile : Actor
{
	Default
	{
		Radius 12;
		Height 10;
		Speed 8;
		Renderstyle "Translucent";
		Alpha 0.75;
		PROJECTILE;
		+RIPPER;
		+NODAMAGETHRUST;
		+FORCERADIUSDMG;
		Obituary "$SGD_OB_DMC";
	}

	States
	{
	Spawn:
		VOIP AABBCCDDEEFFAABBCCDDEEFF 1 Bright { if(CVar.FindCVar('sgd_spawntrails').GetBool() == 1) { A_SpawnItemEx("DMBTrail"); }}
		VOIP AABBCCDDEEFF 1 Bright
		{
			if(CVar.FindCVar('sgd_spawntrails').GetBool() == 1) { A_SpawnItemEx("DMBTrail"); }
			A_RadiusThrust(-220,150);
			A_Explode(10,90,0);
		}
		Goto Spawn+24;
	Death:
		TNT1 A 0
		{
			A_StartSound("BHole/Suck");
			bMOVEWITHSECTOR = true;
		}
		BHOL ABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHI 1
		{
			A_RadiusThrust(-230,250);
			A_Explode(15,100);
		}
		TNT1 A 0 A_StartSound("BHole/Suck");
		BHOL ABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHI 1
		{
			A_RadiusThrust(-230,250);
			A_Explode(15,100);
		}
		TNT1 A 0 A_StartSound("BHole/Suck");
		BHOL ABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHI 1
		{
			A_RadiusThrust(-230,250);
			A_Explode(15,100);
		}
		TNT1 A 0 A_SpawnItemEx("BHSmoke");
		TNT1 A 0 A_SpawnItemEx("BHExplosion");
		TNT1 A 0 A_StartSound("BHole/Explosion");
		BHOL ABCDEFGHI 1 A_FadeOut(0.112);
		Stop;
	}
}

class BHSmoke : DMBTrail
{
	Default
	{
		Scale 2.0;
		+NOCLIP;
		+NOGRAVITY;
		+FORCEXYBILLBOARD;
	}

	States
	{
	Spawn:
		BHXP ABCDEFGHIJKLMNO 2 A_FadeOut(0.03);
		Stop;
	}
}

class BHExplosion : Actor
{
	Default
	{
		+NOCLIP;
		+NOGRAVITY;
		+FORCEXYBILLBOARD;
	}

	States
	{
	Spawn:
		DBX3 AAAAAAAAAA 1
		{
			A_SetScale(scale.x+0.2,scale.y+0.2);
			A_FadeOut(0.1);
		}
		Stop;
	}
}

extend class SGDDarkMatterCannon
{
	action void A_DMCStart()
	{
		A_StartSound("DMC/Charge",CHAN_WEAPON);
		A_AlertMonsters();
	}
	
	action void A_DMCFire()
	{
		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
				return;	
		}
		
		A_SetBlend("99 33 99",0.5,15);
		A_StartSound("DMC/Fire",CHAN_WEAPON);
		SpawnPlayerMissile("DarkMatterProjectile");
		ACS_NamedExecute("FeatTracker",0);
	}
}
