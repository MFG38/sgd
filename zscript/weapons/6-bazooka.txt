class SGDBazooka : SGDWeapon replaces RocketLauncher
{
	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 450;
		Weapon.AmmoType "BazookaAmmo";
		Weapon.AmmoGive 4;
		Weapon.AmmoUse 1;
		Tag "$SGD_WEP_BAZOOKA";
		Inventory.PickupMessage "$SGD_WEP_BAZOOKA";
	}

	States
	{
	Spawn:
		HSGN A -1;
		Stop;
	Select:
		TNT1 AAA 0 A_Raise;
		HSML A 1 A_Raise;
		Goto Select+2;
	Deselect:
		TNT1 AAA 0 A_Lower;
		HSML A 1 A_Lower;
		Goto Deselect+2;
	Ready:
		HSML A 0;
		Goto ReadyLoop;
	Fire:
		HSML B 0 A_JumpIfInventory("Bazooka_FireRate",1,"FireUpgraded");
		HSML B 4 A_BazookaFire;
		HSML CD 4;
		HSML A 6;
		Goto ReadyLoop;
	FireUpgraded:
		HSML B 4 A_BazookaFire;
		HSML CD 4;
		HSML A 0;
		Goto ReadyLoop;
	FinisherFail:
		HSML A 0;
		Goto ReadyLoop;
	Finisher1:
		TNT1 A 0 ACS_NamedExecute("InitFinisher",0,0);
		HSML B 0 A_BazookaSeeker(-4.0);
		HSML B 0 A_BazookaSeeker(0.0);
		HSML B 4 A_BazookaSeeker(4.0);
		HSML CD 4;
		HSML A 10;
		HSML A 0 ACS_NamedExecute("EndFinisher",0);
		Goto ReadyLoop;
	Finisher2:
		TNT1 A 0 ACS_NamedExecute("InitFinisher",0,0);
		HSML B 0 A_BazookaSeeker(-8.0);
		HSML B 0 A_BazookaSeeker(-4.0);
		HSML B 0 A_BazookaSeeker(0.0);
		HSML B 0 A_BazookaSeeker(4.0);
		HSML B 4 A_BazookaSeeker(8.0);
		HSML CD 4;
		HSML A 10;
		HSML A 0 ACS_NamedExecute("EndFinisher",0);
		Goto ReadyLoop;
	Finisher3:
		TNT1 A 0 ACS_NamedExecute("InitFinisher",0,1);
		HSML A 16 A_SpawnItemEx("AirStrikeDummyMarker",320,0,0);
		HSML AAAAAAAAAAAAAAAAAAAA 3 A_SpawnItemEx("BazookaAirStrikeRocket",320+(random(-8,8)*8),random(-8,8)*8,96,0,0,-24);
		HSML A 5;
		HSML A 0 ACS_NamedExecute("EndFinisher",0);
		Goto ReadyLoop;
	}
}

class BazookaRocket : Actor
{
	Default
	{
		Radius 10;
		Height 7;
		DamageFunction (120 + (random(0,4) * 10));
		Speed 24;
		DeathSound "bazooka/explosion";
		PROJECTILE;
	}

	States
	{
	Spawn:
		HRKT A 1 { if(GetCVar("sgd_spawntrails") == 1) { A_SpawnItemEx("SGDProjectileTrail",-24,0,-vel.z); }}
		Loop;
	Death:
		FX08 A 0 A_Explode(150,112);
		FX08 AABBCCDDEEFFGGHH 1 BRIGHT A_FadeOut(0.063);
		Stop;
	}
}

class BazookaSeeker : BazookaRocket
{
	Default
	{
		//+SEEKERMISSILE;
		+SCREENSEEKER;
	}
	
	States
	{
	Spawn:
		HRKT A 1
		{
			A_SeekerMissile(15,20,SMF_LOOK|SMF_PRECISE,128,8);
			if(GetCVar("sgd_spawntrails") == 1) { A_SpawnItemEx("SGDProjectileTrail",-24,0,-vel.z); }
		}
		Loop;
	}
}

class BazookaAirStrikeRocket : BazookaRocket
{
	Default
	{
		Height 24;
		Radius 5;
		Speed 0;
	}
	
	States
	{
	Spawn:
		ASRK A 1;
		Loop;
	}
}

class AirStrikeDummyMarker : Actor
{
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("AirStrikeMarker",64,64,0);
		TNT1 A 0 A_SpawnItemEx("AirStrikeMarker",64,-64,0);
		TNT1 A 0 A_SpawnItemEx("AirStrikeMarker",-64,64,0);
		TNT1 A 0 A_SpawnItemEx("AirStrikeMarker",-64,-64,0);
		TNT1 A 0 A_SpawnItemEx("AirStrikeMarker",0,64,0);
		TNT1 A 0 A_SpawnItemEx("AirStrikeMarker",0,-64,0);
		TNT1 A 0 A_SpawnItemEx("AirStrikeMarker",64,0,0);
		TNT1 A 0 A_SpawnItemEx("AirStrikeMarker",-64,0,0);
		Stop;
	}
}

class AirStrikeMarker : Actor
{
	Default
	{
		Height 8;
		Radius 3;
		Renderstyle "Translucent";
		Alpha 0.5;
		Scale 1.5;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+FLOORHUGGER;
	}
	
	States
	{
	Spawn:
		BPOI DCBABCD 2 BRIGHT;
		Stop;
	}
}

extend class SGDBazooka
{
	action void A_BazookaFire()
	{
		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
				return;	
		}
		
		A_AlertMonsters();
		A_PlaySound("bazooka/fire",CHAN_WEAPON);
		SpawnPlayerMissile("BazookaRocket");
		
		if(!FindInventory("Bazooka_Recoil"))
		{
			A_Recoil(2);
		}
		
		ACS_NamedExecute("FeatTracker",0);
	}
	
	action void A_BazookaSeeker(double ang)
	{
		A_AlertMonsters();
		A_PlaySound("bazooka/fire",CHAN_WEAPON);
		SpawnPlayerMissile("BazookaSeeker",angle+ang);
		ACS_NamedExecute("FeatTracker",0);
	}
}
