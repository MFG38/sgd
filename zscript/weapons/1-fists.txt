class SGDFists : SGDWeapon
{
	Default
	{
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 800;
		Weapon.Kickback 10;
		Tag "$SGD_WEP_FIST";
		+WEAPON.MELEEWEAPON;
		+WEAPON.AMMO_OPTIONAL;
	}

	States
	{
	Select:
		TNT1 AAA 0 A_Raise;
		PUCH A 1 A_Raise;
		Goto Select+2;
	Deselect:
		TNT1 AAA 0 A_Lower;
		PUCH A 1 A_Lower;
		Goto Deselect+2;
	Ready:
		PUCH A 0;
		Goto ReadyLoop;
	Fire:
		PUCH A 0 A_Jump(256,"Punch1","Punch2");
	Punch1:
		PUCH A 2 A_StartSound("fist/swing",CHAN_WEAPON);
		PUCH B 2;
		PUCH L 2 A_FistAttack;
		PUCH CMDNER 2;
		PUCH K 2;
		Goto ReadyLoop;
	Punch2:
		PUCH F 2 A_StartSound("fist/swing",CHAN_WEAPON);
		PUCH G 2;
		PUCH O 2 A_FistAttack;
		PUCH HPIQJ 2;
		PUCH K 2;
		Goto ReadyLoop;
	FinisherFail:
		PUCH A 0;
		Goto ReadyLoop;
	Finisher1:
		TNT1 A 0 A_BeginFinisher(false);
		MLEG ABCD 2;
		MLEG EEEEEEEEEEEE 2 A_Kick('Normal');
		MLEG FGH 2;
		PUCH A 3;
		PUCH A 0 A_EndFinisher;
		Goto ReadyLoop;
	Finisher2:
		TNT1 A 0 A_BeginFinisher(false);
		MLEG A 5;
		MLEG ABCD 2;
		MLEG EEEEEEEEEEEE 2 A_Kick('Sploder');
		MLEG FGH 2;
		PUCH A 6;
		PUCH A 0 A_EndFinisher;
		Goto ReadyLoop;
	Finisher3:
		TNT1 A 0 A_BeginFinisher(false);
		PUCH A 0 A_GiveInventory("FistFinisherTimeFreeze",1);
		//PUCH A 0 ACS_NamedExecute("FistFinisher3Timer",0);
		Goto ReadyLoop;
	}
}

class FistFinisherTimeFreeze : PowerTimeFreezer
{
	Default { Powerup.Duration 175; }
	
	override void InitEffect()
	{
		super.InitEffect();
		
		PlayerInfo p = players[consoleplayer];
		Shader.SetEnabled(p,"OldVideo",true);
	}
	
	override void EndEffect()
	{
		super.EndEffect();
		
		PlayerInfo p = players[consoleplayer];
		Shader.SetEnabled(p,"OldVideo",false);
	}
}

extend class SGDFists
{	
	action void A_FistAttack()
	{
		FTranslatedLineTarget t;
		
		if (player != null)
		{
			Weapon weap = player.ReadyWeapon;
			if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
			{
				if (!weap.DepleteAmmo (weap.bAltFire))
					return;
			}
		}

		int dmg = random(3,5) * 5;
		if(FindInventory("PowerStrength")) { dmg *= 8; }

		LineAttack(angle,64,pitch,dmg,'Melee',"BulletPuff",LAF_ISMELEEATTACK,t);
		ACS_NamedExecute("FeatTracker",0);
		
		if(t.linetarget)
		{
			A_StartSound("*fist",CHAN_WEAPON);
			A_AlertMonsters(256);
		}
	}
	
	action void A_Kick(name damagetype)
	{
		FTranslatedLineTarget t;
		
		if (player != null)
		{
			Weapon weap = player.ReadyWeapon;
			if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
			{
				if (!weap.DepleteAmmo (weap.bAltFire))
					return;
			}
		}

		A_Recoil(-10);
		LineAttack(angle,64,pitch,100,damagetype,"BulletPuff",LAF_ISMELEEATTACK,t);
		//A_RadiusThrust(CountInv("FistFinisherThrustScaler") * 2000,64,RTF_NOTMISSILE);
		ACS_NamedExecute("FeatTracker",0);
		
		if(t.linetarget)
		{
			A_StartSound("*fist",CHAN_WEAPON);
			A_AlertMonsters(256);
		}
	}
}
