class SGDFists : SGDWeapon replaces Fist
{
	Default
	{
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 800;
		Weapon.Kickback 10;
		Tag "$SGD_WEP_FIST";
		+WEAPON.MELEEWEAPON;
		+WEAPON.AMMO_OPTIONAL;
	}

	States
	{
	Select:
		TNT1 AAA 0 A_Raise;
		PUCH A 1 A_Raise;
		Goto Select+2;
	Deselect:
		TNT1 AAA 0 A_Lower;
		PUCH A 1 A_Lower;
		Goto Deselect+2;
	Ready:
		PUCH A 0;
		Goto ReadyLoop;
	Fire:
		PUCH A 0 A_JumpIfInventory("FistSwitcher",1,"SecondPunch");
		PUCH A 2 A_PlaySound("fist/swing",CHAN_WEAPON);
		PUCH B 2;
		PUCH L 2 A_FistAttack;
		PUCH CMDNER 2;
		PUCH K 2
		{
			A_GiveInventory("FistSwitcher",1);
			A_ReFire();
		}
		Goto ReadyLoop;
	SecondPunch:
		PUCH F 2 A_PlaySound("fist/swing",CHAN_WEAPON);
		PUCH G 2;
		PUCH O 2 A_FistAttack;
		PUCH HPIQJ 2;
		PUCH K 2
		{
			A_TakeInventory("FistSwitcher",1);
			A_ReFire();
		}
		Goto ReadyLoop;
	FinisherFail:
		PUCH A 0;
		Goto ReadyLoop;
	Finisher1:
		TNT1 A 0 ACS_NamedExecute("InitFinisher",0,1);
		TNT1 A 0 A_GiveInventory("FistFinisherThrustScaler",1);
		MLEG ABC 2;
		MLEG DE 2 A_Kick(0.0);
		MLEG FGH 2;
		PUCH A 3;
		PUCH A 0 A_TakeInventory("FistFinisherThrustScaler",0x7FFFFFFF);
		PUCH A 0 ACS_NamedExecute("EndFinisher",0);
		Goto ReadyLoop;
	Finisher2:
		TNT1 A 0 ACS_NamedExecute("InitFinisher",0,1);
		TNT1 A 0 A_GiveInventory("FistFinisherThrustScaler",2);
		MLEG A 5;
		MLEG AB 2;
		MLEG C 2 A_Kick(-4.0);
		MLEG DE 2 A_Kick(0.0);
		MLEG F 2 A_Kick(4.0);
		MLEG GH 2;
		PUCH A 6;
		PUCH A 0 A_TakeInventory("FistFinisherThrustScaler",0x7FFFFFFF);
		PUCH A 0 ACS_NamedExecute("EndFinisher",0);
		Goto ReadyLoop;
	Finisher3:
		TNT1 A 0 ACS_NamedExecute("InitFinisher",0,1);
		TNT1 A 0 A_GiveInventory("FistFinisherThrustScaler",3);
		MLEG A 15;
		MLEG ABCD 1;
		MLEG E 0 A_Kick(-20.0);
		MLEG E 0 A_Kick(-16.0);
		MLEG E 0 A_Kick(-12.0);
		MLEG E 0 A_Kick(-8.0);
		MLEG E 0 A_Kick(-4.0);
		MLEG E 0 A_Kick(0.0);
		MLEG E 0 A_Kick(4.0);
		MLEG E 0 A_Kick(8.0);
		MLEG E 0 A_Kick(12.0);
		MLEG E 0 A_Kick(16.0);
		MLEG E 20 A_Kick(20.0);
		MLEG DCBA 1;
		PUCH A 0 A_TakeInventory("FistFinisherThrustScaler",0x7FFFFFFF);
		PUCH A 0 ACS_NamedExecute("EndFinisher",0);
		Goto ReadyLoop;
	}
}

extend class SGDFists
{	
	action void A_FistAttack()
	{
		FTranslatedLineTarget t;
		
		if (player != null)
		{
			Weapon weap = player.ReadyWeapon;
			if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
			{
				if (!weap.DepleteAmmo (weap.bAltFire))
					return;
			}
		}

		int dmg = random(2,5) * 5;

		LineAttack(angle,64,pitch,dmg,'Melee',"BulletPuff",LAF_ISMELEEATTACK,t);
		ACS_NamedExecute("FeatTracker",0);
		
		if(t.linetarget)
		{
			A_PlaySound("*fist",CHAN_WEAPON);
			A_AlertMonsters(256);
		}
	}
	
	action void A_Kick(double ang)
	{
		FTranslatedLineTarget t;
		
		if (player != null)
		{
			Weapon weap = player.ReadyWeapon;
			if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
			{
				if (!weap.DepleteAmmo (weap.bAltFire))
					return;
			}
		}

		int dmg = 50 + (random(0,5) * 10);

		LineAttack(angle+ang,64,pitch,dmg,'Melee',"BulletPuff",LAF_ISMELEEATTACK,t);
		A_RadiusThrust(CountInv("FistFinisherThrustScaler") * 2000,64,RTF_NOTMISSILE);
		ACS_NamedExecute("FeatTracker",0);
		
		if(t.linetarget)
		{
			A_PlaySound("*fist",CHAN_WEAPON);
			A_AlertMonsters(256);
		}
	}
}
