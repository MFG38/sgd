class SGDFlamethrower : SGDWeapon replaces PlasmaRifle
{
	Default
	{
		Weapon.SlotNumber 7;
		Weapon.SelectionOrder 180;
		Weapon.AmmoType "FTAmmo";
		Weapon.AmmoGive 100;
		Weapon.AmmoUse 1;
		Tag "$SGD_WEP_FLAMETHROWER";
		Inventory.PickupMessage "$SGD_WEP_FLAMETHROWER";
	}
	
	States
	{
	Spawn:
		WFLM A -1;
		Stop;
	Select:
		TNT1 AAA 0 A_Raise;
		FLMG A 1 A_Raise;
		Goto Select+2;
	Deselect:
		TNT1 AAA 0 A_Lower;
		FLMG A 1 A_Lower;
		Goto Deselect+2;
	Ready:
		FLMG A 0;
		Goto ReadyLoop;
	ReadyLoop:
		FLMG BCD 2
		{
			A_WeaponReady();
			ACS_NamedExecute("FeatTracker",0);
		}
		Loop;
	Fire:
		FLMG E 2 A_FlamerAttack;
		FLMG F 2 A_FlamerAttack;
		FLMG G 2 A_FlamerAttack;
		FLMG H 2 A_FlamerAttack;
		FLMG H 0 A_ReFire;
		FLMG A 4;
		Goto ReadyLoop;
	FinisherFail:
		FLMG A 0;
		Goto ReadyLoop;
	Finisher1:
		TNT1 A 0 A_BeginFinisher(false);
		FLMG EF 2;
		FLMG G 0 A_FlamerFinisher(-12.0);
		FLMG G 0 A_FlamerFinisher(-8.0);
		FLMG G 0 A_FlamerFinisher(-4.0);
		FLMG G 0 A_FlamerFinisher(0.0);
		FLMG G 0 A_FlamerFinisher(4.0);
		FLMG G 0 A_FlamerFinisher(8.0);
		FLMG G 2 A_FlamerFinisher(12.0);
		FLMG HBCD 2;
		FLMG A 0 A_EndFinisher;
		Goto ReadyLoop;
	Finisher2:
		TNT1 A 0 A_BeginFinisher(false);
		FLMG EF 2;
		FLMG G 0 A_FlamerFinisher(-15.0);
		FLMG G 0 A_FlamerFinisher(-12.0);
		FLMG G 0 A_FlamerFinisher(-9.0);
		FLMG G 0 A_FlamerFinisher(-6.0);
		FLMG G 0 A_FlamerFinisher(-3.0);
		FLMG G 0 A_FlamerFinisher(0.0);
		FLMG G 0 A_FlamerFinisher(3.0);
		FLMG G 0 A_FlamerFinisher(6.0);
		FLMG G 0 A_FlamerFinisher(9.0);
		FLMG G 0 A_FlamerFinisher(12.0);
		FLMG G 2 A_FlamerFinisher(15.0);
		FLMG HBCDBCD 2;
		FLMG A 0 A_EndFinisher;
		Goto ReadyLoop;
	Finisher3:
		TNT1 A 0 A_BeginFinisher(true);
		FLMG EF 2;
		FLMG G 2 A_FlamerFinisher(0.0);
		FLMG H 2 A_FlamerFinisher(0.0);
		FLMG E 2 A_FlamerFinisher(0.0);
		FLMG F 2 A_FlamerFinisher(0.0);
		FLMG G 2 A_FlamerFinisher(0.0);
		FLMG H 2 A_FlamerFinisher(0.0);
		FLMG E 2 A_FlamerFinisher(0.0);
		FLMG F 2 A_FlamerFinisher(0.0);
		FLMG G 2 A_FlamerFinisher(0.0);
		FLMG H 2 A_FlamerFinisher(0.0);
		FLMG E 2 A_FlamerFinisher(0.0);
		FLMG F 2 A_FlamerFinisher(0.0);
		FLMG G 2 A_FlamerFinisher(0.0);
		FLMG H 2 A_FlamerFinisher(0.0);
		FLMG E 2 A_FlamerFinisher(0.0);
		FLMG F 2 A_FlamerFinisher(0.0);
		FLMG G 2 A_FlamerFinisher(0.0);
		FLMG H 2 A_FlamerFinisher(0.0);
		FLMG BCDBCDBCDBCD 2;
		FLMG A 0 A_EndFinisher;
		Goto ReadyLoop;
	}
}

class FlamethrowerFire : Actor
{
	Default
	{
		Height 8;
		Radius 4;
		DamageFunction (random(5,8) * 2);
		Speed 10;
		RenderStyle "Translucent";
		Alpha 0.8;
		DeathSound "flamethrower/hit";
		DamageType "Fire";
		PROJECTILE;
		+NODAMAGETHRUST;
	}
	
	States
	{
	Spawn:
		FRFX AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 BRIGHT A_FadeOut(0.032);
		Stop;
	Death:
		TNT1 A 0;
		Stop;
	}
}

class FlamethrowerFire_Linger : FlamethrowerFire
{
	States
	{
	Spawn:
		FRFX AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 BRIGHT A_FadeOut(0.032);
		Stop;
	Death:
		FLME A 0
		{
			bNOGRAVITY = false;
		}
		FLME A 0 A_PlaySound("fx/fireburn");
		FLME ABCDEFGHIJKLMN 2 A_Explode(5,32,XF_NOTMISSILE);
		FLME A 0 A_PlaySound("fx/fireburn");
		FLME ABCDEFGHIJKLMN 2 A_Explode(5,32,XF_NOTMISSILE);
		FLME A 0 A_PlaySound("fx/fireburn");
		FLME ABCDEFGHIJKLMN 2 A_Explode(5,32,XF_NOTMISSILE);
		FLME A 0 A_PlaySound("fx/fireburn");
		FLME ABCDEFGHIJKLMN 2 A_Explode(5,32,XF_NOTMISSILE);
		FLME A 0 A_PlaySound("fx/fireburn");
		FLME ABCDEFGHIJKLMN 2
		{
			A_Explode(4,32,XF_NOTMISSILE);
			A_FadeOut(0.018);
		}
		FLME A 0 A_PlaySound("fx/fireburn");
		FLME ABCDEFGHIJKLMN 2
		{
			A_Explode(3,32,XF_NOTMISSILE);
			A_FadeOut(0.018);
		}
		FLME A 0 A_PlaySound("fx/fireburn");
		FLME ABCDEFGHIJKLMN 2 A_FadeOut(0.018);
		FLME A 0 A_PlaySound("fx/fireburn");
		FLME ABCDEFGHIJKLMN 2 A_FadeOut(0.018);
		Stop;
	}
}

class FlamethrowerFinisherFlame : Actor
{
	Default
	{
		Height 40;
		Radius 8;
		DamageFunction (20 + (random(0,5) * 4));
		Speed 15;
		RenderStyle "Translucent";
		Alpha 0.8;
		DeathSound "flamethrower/hit";
		DamageType "Fire";
		PROJECTILE;
		+NODAMAGETHRUST;
		+FLOORHUGGER;
	}
	
	States
	{
	Spawn:
		FLME AABBCCDDEEFFGGHHIIJJKKLLMMNN 1 BRIGHT { if(GetCVar("sgd_spawntrails") == 1) { A_SpawnItemEx("FlamethrowerFinisherFlameTrail"); }}
		Loop;
	Death:
		TNT1 A 0;
		Stop;
	}
}

extend class SGDFlamethrower
{
	action void A_FlamerAttack()
	{
		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
				return;	
		}
		
		A_AlertMonsters();
		A_PlaySound("flamethrower/fire",CHAN_WEAPON);
		
		if(FindInventory("Flamethrower_FlameLinger"))
		{
			SpawnPlayerMissile("FlamethrowerFire_Linger");
		}
		else
		{
			SpawnPlayerMissile("FlamethrowerFire");
		}
		
		ACS_NamedExecute("FeatTracker",0);
	}
	
	action void A_FlamerFinisher(double ang)
	{
		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
				return;	
		}
		
		A_AlertMonsters();
		A_PlaySound("flamethrower/fire",CHAN_WEAPON);
		SpawnPlayerMissile("FlamethrowerFinisherFlame",angle+ang);
		ACS_NamedExecute("FeatTracker",0);
	}
}
