class SGDShotgun : SGDWeapon
{
	Default
	{
		Weapon.SlotNumber 3;
		Weapon.SelectionOrder 280;
		Weapon.AmmoType "ShellAmmo";
		Weapon.AmmoGive 10;
		Weapon.AmmoUse 1;
		Tag "$SGD_WEP_SHOTGUN";
		Inventory.PickupMessage "$SGD_WEP_SHOTGUN";
	}

	States
	{
	Spawn:
		DNSP A -1;
		Stop;
	Select:
		DNSG A 1 A_FastRaise;
		Goto Select;
	Deselect:
		DNSG A 1 A_FastLower;
		Goto Deselect;
	Ready:
		DNSG A 0;
		Goto ReadyLoop;
	Fire:
		DNSG A 3;
		DNSG B 1 A_ShotgunFire;
		DNSG CDEDCB 1 bright;
		DNSG A 3;
		DNSG FGH 1;
		DNSG I 2 A_StartSound("DukeShotgun/Pump");
		DNSG JKLKJI 2;
		DNSG HGF 1;
		DNSG A 7 A_ReFire;
		Goto ReadyLoop;
	FinisherFail:
		DNSG A 0;
		Goto ReadyLoop;
	Finisher1:		// Credit to InfernalMonsoon for coming up with this!
		DNSG A 3 A_BeginFinisher(false);
		DNSG B 1 A_ShotgunSlug;
		DNSG CDEDCB 1 bright;
		DNSG AAAAAA 3;
		DNSG FGH 1;
		DNSG I 2 A_StartSound("DukeShotgun/Pump");
		DNSG JKLKJI 2;
		DNSG HGF 1;
		DNSG A 7 A_EndFinisher;
		Goto ReadyLoop;
	Finisher2:		// Credit to InfernalMonsoon for coming up with this!
		DNSG A 3 A_BeginFinisher(false);
		DNSG B 0 A_ShotgunBouncySlug(-15.0);
		DNSG B 0 A_ShotgunBouncySlug(0.0);
		DNSG B 1 A_ShotgunBouncySlug(15.0);
		DNSG CDEDCB 1 bright;
		DNSG AAAAAA 3;
		DNSG FGH 1;
		DNSG I 2 A_StartSound("DukeShotgun/Pump");
		DNSG JKLKJI 2;
		DNSG HGF 1;
		DNSG A 7 A_EndFinisher;
		Goto ReadyLoop;
	Finisher3:		// A slightly modified version of an idea by InfernalMonsoon.
		DNSG A 0 A_BeginFinisher(false);
		DNSG A 15
		{
			A_StartSound("drone/spawn");
			A_SpawnItemEx("HelperDrone",32,0,35);
		}
		DNSG A 1 A_EndFinisher;
		Goto ReadyLoop;
	}
}

class ShotgunSlug : Actor
{
	Default
	{
		Radius 4;
		Height 8;
		Speed 50;
		DamageFunction 128 + (random(0,4) * 16);
		DamageType "ShotgunSlug";
		Renderstyle "Translucent";
		Alpha 0.8;
		PROJECTILE;
		+FLATSPRITE;
		+RIPPER;
		Obituary "$SGD_OB_SGSLUG";
	}
	
	States
	{
	Spawn:
		SLUG A 1
		{
			A_SpawnItemEx("ShotgunSlugTrail");
			A_SpawnItemEx("ShotgunSlugCone");
		}
		Loop;
	Death:
		TNT1 A 1;
		Stop;
	}
}

class ShotgunBouncySlug : ShotgunSlug
{
	Default
	{
		DamageType "ShotgunSlug";
		BounceType "Hexen";
		BounceCount 8;
		ProjectileKickBack 0;
		BounceSound "fx/slugbounce";
		BounceFactor 1.0;
		WallBounceFactor 1.0;
		-ALLOWBOUNCEONACTORS;
		-BOUNCEONACTORS;
		+HITOWNER;		// For a bit of extra risk.
		+NODAMAGETHRUST;
	}
}

class ShotgunSlugTrail : SGDProjectileTrail
{
	Default
	{
		Alpha 0.8;
		Scale 1.0;
		+FLATSPRITE;
	}
	
	States
	{
	Spawn:
		SLUG AAAAAAAA 1 A_FadeOut(0.1);
		Stop;
	}
}

class ShotgunSlugCone : SGDProjectileTrail
{
	Default
	{
		Radius 10;
		Height 20;
		Alpha 0.64;
		Scale 0.5;
	}
	
	States
	{
	Spawn:
		SLGT AAAAAAAA 1
		{
			A_SetScale(scale.x+0.05,scale.y+0.05);
			A_FadeOut(0.08);
		}
		Stop;
	}
}

class HelperDrone : Actor
{
	Default
	{
		Health 440;
		Radius 13;
		Height 22;
		Speed 15;
		Mass 5000;
		DeathSound "drone/explosion";
		+SOLID;
		+SHOOTABLE;
		+FLOORCLIP;
		+NOGRAVITY;
		+FLOAT;
		+CANPASS;
		+FRIENDLY;
		+DONTFALL;
		+NOBLOOD;
		+NOTELEPORT;	// Because we don't want the drones to disappear "mysteriously".
	}
	
	States
	{
	Spawn:
		HDRN A 20;
		Goto IdleLoop;
	IdleLoop:
		HDRN A 0
		{
			A_DamageSelf(5);	// Because we don't want the drones to wander eternally.
			A_Look();
		}
		HDRN AAAAAAAAA 4 A_Wander;
		Loop;
	See:
		HDRN A 0 A_DamageSelf(5);
		HDRN AAAAAAAAA 4 A_Chase;
		Loop;
	Missile:
		HDRN A 4 A_FaceTarget;
		HDRN A 8 bright
		{
			A_StartSound("DukeShotgun/slug");
			A_SpawnProjectile("ShotgunSlug",12.0,flags:CMF_CHECKTARGETDEAD);
		}
		Goto See;
	Death:
		HDRN A 0 A_StartSound("drone/death");
		HDRN AAAAAAAAAAAAAAAAAAAAAAAAA 2 A_SetAngle(angle + (random(1,3) * 45));
		TNT1 A 1
		{
			A_ScreamAndUnblock();
			A_SpawnItemEx("Explosion",0,0,30);
			A_Explode(320,128);
		}
		Stop;
	}
}

extend class SGDShotgun
{
	action void A_ShotgunFire()
	{
		if (player == null)
		{
			return;
		}

		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
			{
				return;
			}	
			player.SetPsprite(PSP_FLASH, weap.FindState('Flash'), true);
		}
		player.mo.PlayAttacking2 ();

		A_AlertMonsters();
		A_StartSound("DukeShotgun/fire",CHAN_WEAPON);

		for(int i = 0; i < 12; i++)
		{
			int dmg = random(14,18);
			int range;
			double xspread;
			double yspread;
			
			if(FindInventory("Shotgun_Range"))
			{
				range = 480;
			}
			else
			{
				range = 384;
			}
			
			switch(i)
			{
			case 0:
				xspread = angle;
				yspread = pitch;
				break;
			default:
				if(FindInventory("Shotgun_Cone"))
				{
					xspread = angle + random2() * (11.25 / 256);
					yspread = pitch + random2() * (8.65 / 256);
				}
				else
				{
					xspread = angle + random2() * (16.65 / 256);
					yspread = pitch + random2() * (13.5 / 256);
				}
				break;
			}

			LineAttack(xspread,range,yspread,dmg,'Normal',"BulletPuff");
		}
		ACS_NamedExecute("FeatTracker",0);
	}
	
	action void A_ShotgunSlug()
	{
		if (player == null)
		{
			return;
		}

		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
			{
				return;
			}	
			player.SetPsprite(PSP_FLASH, weap.FindState('Flash'), true);
		}
		player.mo.PlayAttacking2 ();
		
		A_AlertMonsters();
		A_StartSound("DukeShotgun/slug",CHAN_WEAPON);
		SpawnPlayerMissile("ShotgunSlug");
		ACS_NamedExecute("FeatTracker",0);
	}
	
	action void A_ShotgunBouncySlug(double ang)
	{
		if (player == null)
		{
			return;
		}

		Weapon weap = player.ReadyWeapon;
		if (weap != null && invoker == weap && stateinfo != null && stateinfo.mStateType == STATE_Psprite)
		{
			if (!weap.DepleteAmmo (weap.bAltFire, true, 1))
			{
				return;
			}	
			player.SetPsprite(PSP_FLASH, weap.FindState('Flash'), true);
		}
		player.mo.PlayAttacking2 ();
		
		A_AlertMonsters();
		A_StartSound("DukeShotgun/slug",CHAN_WEAPON);
		SpawnPlayerMissile("ShotgunBouncySlug",angle+ang);
		ACS_NamedExecute("FeatTracker",0);
	}
}
