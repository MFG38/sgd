class SGDArgentRifle : SGDWeapon
{
	Default
	{
		Weapon.SlotNumber 8;
		Weapon.SelectionOrder 100;
		Weapon.AmmoType "ArgentRifleHeat";
		Weapon.AmmoGive 0;
		Weapon.AmmoUse 0;
		Tag "$SGD_WEP_ARGENTRIFLE";
		Inventory.PickupMessage "$SGD_WEP_ARGENTRIFLE";
		//+WEAPON.NOAUTOAIM;
	}

	States
	{
	Spawn:
		STPP A -1;
		Stop;
	Select:
		TNT1 AAA 0 A_Raise;
		STPO E 1 A_Raise;
		Goto Select+2;
	Deselect:
		TNT1 AAA 0 A_Lower;
		STPO E 1 A_Lower;
		Goto Deselect+2;
	Ready:
		STPO A 0;
		Goto ReadyLoop;
	ReadyLoop:
		STPO A 0 A_JumpIfInventory("ArgentRifle_Cooldown",1,"ReadyLoopUpgraded");
		STPO A 0 A_TakeInventory("ArgentRifleHeat",1);
		STPO ABCABCABCABC 2
		{
			A_WeaponReady();
			ACS_NamedExecute("FeatTracker",0);
		}
		Loop;
	ReadyLoopUpgraded:
		STPO A 0 A_TakeInventory("ArgentRifleHeat",1);
		STPO ABCABCABC 2
		{
			A_WeaponReady();
			ACS_NamedExecute("FeatTracker",0);
		}
		Loop;
	Fire:
		STPO D 0 A_JumpIfInventory("ArgentRifleHeat",100,"OverheatLoop");
		STPO D 1 A_GiveInventory("ArgentRifleHeat",2);
		STPO F 1 A_ArgentRifleFire;
		STPO GH 1;
		STPO H 0 A_ReFire;
		Goto ReadyLoop;
	OverheatLoop:
		STPO E 1
		{
			A_PlaySound("argentrifle/overheat",CHAN_WEAPON);
			A_TakeInventory("ArgentRifleHeat",1);
		}
		STPO EEEEEEE 1 A_TakeInventory("ArgentRifleHeat",1);
		STPO E 0 A_JumpIfInventory("ArgentRifleHeat",65,"OverheatLoop");
		Goto ReadyLoop;
	FinisherFail:
		STPO A 0;
		Goto ReadyLoop;
	Finisher1:
		TNT1 A 0 A_BeginFinisher(false);
		STPO D 0 A_PlaySound("argentrifle/laser",CHAN_WEAPON);
		STPO DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD 1 A_ArgentRifleLaser(0.0,0);
		STPO A 0 A_EndFinisher;
		Goto ReadyLoop;
	Finisher2:
		TNT1 A 0 A_BeginFinisher(false);
		STPO D 0 A_PlaySound("argentrifle/laser",CHAN_WEAPON);
		STPO DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD 1
		{
			A_ArgentRifleLaser(-20.0,-4);
			A_ArgentRifleLaser(0.0,0);
			A_ArgentRifleLaser(20.0,4);
		}
		STPO A 0 A_EndFinisher;
		Goto ReadyLoop;
	Finisher3:
		TNT1 A 0 A_BeginFinisher(false);
		STPO D 2;
		STPO F 2 A_ArgentBomb;
		STPO GHABC 2;
		STPO A 0 A_EndFinisher;
		Goto ReadyLoop;
	}
}

class ArgentRifleBall : Actor
{
	Default
	{
		Height 10;
		Radius 5;
		DamageFunction (random(4,6) * 5);
		Speed 25;
		RenderStyle "Translucent";
		Alpha 0.75;
		DeathSound "argentrifle/ballhit";
		PROJECTILE;
		+RANDOMIZE;
	}

	States
	{
	Spawn:
		PLSS AABB 1 Bright { if(CVar.FindCVar('sgd_spawntrails').GetBool() == 1) { A_SpawnItemEx("SGDProjectileTrail",-20,0,-vel.z); }}
		Loop;
	Death:
		PLSE ABCDE 3 Bright;
		Stop;
	}
}

class ArgentRifleLaser : Actor
{
	Default
	{
		Height 10;
		Radius 5;
		DamageFunction (75 + (random(0,5) * 5));
		Speed 55;
		RenderStyle "Translucent";
		Alpha 0.5;
		DeathSound "argentrifle/ballhit";
		PROJECTILE;
		//+RANDOMIZE;
	}

	States
	{
	Spawn:
		ARLS A 1 Bright
		{
			A_SpawnItemEx("ArgentRifleLaserTrail",-(0.2*vel.x),0,-(0.2*vel.z));
			A_SpawnItemEx("ArgentRifleLaserTrail",-(0.4*vel.x),0,-(0.4*vel.z));
			A_SpawnItemEx("ArgentRifleLaserTrail",-(0.6*vel.x),0,-(0.6*vel.z));
			A_SpawnItemEx("ArgentRifleLaserTrail",-(0.8*vel.x),0,-(0.8*vel.z));
		}
		Loop;
	Death:
		ARLS A 0;
		Stop;
	}
}

class ArgentBomb : Actor
{
	Default
	{
		Height 24;
		Radius 12;
		Speed 15;
		Renderstyle "Translucent";
		Alpha 0.65;
		DeathSound "argentrifle/bombex";
		DamageType "ArgentBomb";
		PROJECTILE;
		+RANDOMIZE;
		-NOGRAVITY;
		+NODAMAGETHRUST;
		Gravity 0.35;
	}
	
	States
	{
	Spawn:
		ABOM AAABBB 1 Bright;
		Loop;
	Death:
		ABOM CCCDDDEEEFFFGGG 1 Bright
		{
			A_Explode(225,80,XF_NOTMISSILE);
			A_SpawnItemEx("ArgentBombExplosion",random(1,4)*48,0,0,angle:random(0,11)*30);
		}
		Stop;
	}
}

class ArgentBombExplosion : Actor
{
	Default
	{
		Height 16;
		Radius 8;
		Speed 0;
		Renderstyle "Translucent";
		Alpha 0.65;
		DamageType "ArgentBomb";
		PROJECTILE;
		+NODAMAGETHRUST;
	}
	
	States
	{
	Spawn:
		ABOM CCCDDDEEEFFFGGG 1 Bright A_Explode(225,80,XF_NOTMISSILE);
		Stop;
	}
}

extend class SGDArgentRifle
{
	action void A_ArgentRifleFire()
	{
		A_AlertMonsters();
		A_PlaySound("argentrifle/fire",CHAN_WEAPON);
		SpawnPlayerMissile("ArgentRifleBall");
		ACS_NamedExecute("FeatTracker",0);
	}
	
	action void A_ArgentRifleLaser(double ang, int xoffset)
	{
		A_AlertMonsters();
		SpawnPlayerMissile("ArgentRifleLaser",angle+ang,xoffset);
		ACS_NamedExecute("FeatTracker",0);
	}
	
	action void A_ArgentBomb()
	{
		A_AlertMonsters();
		A_PlaySound("argentrifle/bomb");
		SpawnPlayerMissile("ArgentBomb");
	}
}
