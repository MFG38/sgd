class FieldMedPack : CustomInventory
{
	Default
	{
		Inventory.Amount 100;
		Inventory.MaxAmount 100;
		Inventory.PickupMessage "$SGD_PU_FIELDMED";
		Inventory.Icon "ICON_FMP";
		Inventory.ForbiddenTo "DemonizedPlayer";
		+INVENTORY.INVBAR;
		+INVENTORY.UNDROPPABLE;
		//+INVENTORY.IGNORESKILL;
	}

	States
	{
	Spawn:
		MBPK A -1;
		Stop;
	Use:
		TNT1 A 0
		{
			if(invoker.owner.Health < 100)
			{
				int AmtToHeal = 100 - invoker.owner.Health;
				if(AmtToHeal >= CountInv("FieldMedPack"))
				{
					A_GiveInventory("Health",CountInv("FieldMedPack"));
					A_SetInventory("FieldMedPack",0);
				}
				else
				{
					A_GiveInventory("Health",AmtToHeal);
					A_SetInventory("FieldMedPack",CountInv("FieldMedPack") - AmtToHeal);
				}
				
				if(CVar.FindCVar('sgd_fieldmedsound').GetBool() == true)
				{
					A_StartSound("fieldmed/use",CHAN_BODY);
				}
			}
			else
			{
				A_Log(StringTable.Localize("$SGD_WRN_CANTUSEFIELDMED"));
			}
			//ACS_NamedExecute("FieldMedHeal",0);
		}
		Fail;
	}
}
