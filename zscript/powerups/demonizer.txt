// This code is for stuff related to the Demonizer powerup itself. See "demonized_player.txt"
// for the player morph and powered-up fists.

class DemonizerOrb : CustomInventory
{
	Default
	{
		Scale 0.75;
		Inventory.MaxAmount 1;
		Inventory.Icon "ICON_DEM";
		Inventory.PickupMessage "$SGD_PU_DEMONIZER";
		Inventory.ForbiddenTo "DemonizedPlayer";
		+INVENTORY.INVBAR;
	}

	States
	{
	Spawn:
		INSS ABCD 6 Light("L_DemonizerPickup");
		Loop;
	Use:
		TNT1 A 0 A_GiveInventory("DemonizerMorph",1);
		Stop;
	}
}

class DemonizerMorph : PowerMorph
{
	Default
	{
		PowerMorph.PlayerClass "DemonizedPlayer";
		PowerMorph.MorphStyle MRF_FULLHEALTH;
		PowerMorph.MorphFlash "DemonizerMorphFX";
		PowerMorph.UnMorphFlash "DemonizerMorphFX";
		Powerup.Color "RedMap";
		Powerup.Duration -60;
	}
	
	override void InitEffect()
	{
		super.InitEffect();
		owner.A_GiveInventory("DemonizerTimer",2100);
		
		S_ChangeMusic("BFG_DIV");
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		owner.A_TakeInventory("DemonizerTimer",1);
	}
	
	override bool IsBlinking()
	{
		return false;
	}
	
	override void EndEffect()
	{
		super.EndEffect();
		owner.A_TakeInventory("DemonizerTimer",1);
		
		S_ChangeMusic("*");
	}
}
